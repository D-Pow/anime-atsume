import java.lang.reflect.Field;
import java.util.Optional;
import java.util.stream.Collectors;

import javax.tools.ToolProvider

plugins {
    id 'java'
    id 'idea'
    id 'war'
    // adding plugins here removes the need to declare them in buildscript {}
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    // If running as a standalone JavaFX app with Java >= v11, add this plugin
    // id 'org.openjfx.javafxplugin' version '0.0.10'
}

group 'org.animeatsume'
version '1.0'

// Code was originally written for Java v8 so ensure it can run with these configs
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Adds relevant directories for the app to the global scope.
//
// `ext` is more-or-less equivalent to global vars.
//
// See:
//  - https://stackoverflow.com/questions/61842857/passing-environment-variables-from-build-gradle-to-custom-plugin/61853116#61853116
//  - https://docs.gradle.org/current/dsl/org.gradle.api.Project.html#org.gradle.api.Project.extraproperties
//  - https://docs.gradle.org/current/dsl/org.gradle.api.Project.html#N14E9A)
ext {
    javaHome = "${System.properties['java.home']}"

    if (!new File(javaHome).exists()) {
        javaHome = "${JAVA_HOME}" || System.getenv('JAVA_HOME');
    }

    javaLibDir = "${javaHome}/lib";

    if (!new File(javaLibDir).exists()) {
        javaLibDir = "${javaHome}/../lib";
    }

    rootDirPath = cliCmd("git rev-parse --show-toplevel");
    serverDirPath = "${projectDir}";
    clientDirPath = "${serverDirPath}/../client";
    clientDirDepsPath = "${clientDirPath}/node_modules";
    serverWebAssetDirPath = "${serverDirPath}/src/main/resources/public";
    warOutputDir = "${project.buildDir}/${project.libsDirName}";
    dbName = 'anime_atsume.db';
}

task printVersion {
    println rootProject.artifacts
}

task printCommands {
    // Alternatives:
    //  - configurations.forEach(config -> { println(config.name); });
    //  - ./gradlew --console plain dependencies 2>/dev/null | grep -Eio '^[a-z]+ - .*'
    //
    // See:
    //  - https://stackoverflow.com/questions/38362977/how-to-list-all-tasks-for-the-master-project-only-in-gradle/40188539#40188539
    //  - https://stackoverflow.com/questions/41173616/how-can-i-get-a-list-of-all-configurations-for-a-gradle-project
    doLast {
        println(cliCmd("./gradlew -q tasks --all"));
    }
}


repositories {
    mavenCentral()
}

dependencies {
    // JavaFX was removed in Java 11 so ensure it's installed (uses OpenJDK)
    implementation 'org.openjfx:javafx:18.0.2'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.projectlombok:lombok:1.18.24'

    // implementation 'mysql:mysql-connector-java'
    implementation 'org.xerial:sqlite-jdbc:3.30.1'
    implementation 'com.github.gwenn:sqlite-dialect:0.1.0'

    // HTML parser
    implementation 'org.jsoup:jsoup:1.13.1'

    /*
     * Need a webdriver library to load and scrape Kissanime pages.
     * Options for webdriver libraries: https://gist.github.com/alexislucena/ce3bdccf334f5cd0da41920725947b5c
     *
     * Decided to use [ui4j](https://webfolderio.github.io/ui4j/) because it's lightweight
     * in comparison to Selenium and uses native Java unlike PhantomJS.
     * Using v3.0.0 because it ironically has more features than 4.0.0
     * (see https://github.com/webfolderio/ui4j/tree/3.0.0)
     */
    implementation 'io.webfolder:ui4j-webkit:3.0.0'

    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

    // Not sure exactly why but occasionally `java/lib/tools.jar` won't be loaded (at least on Mac).
    // Thus, load it dynamically via the native Java API.
    // Likely due to certain features used in this `build.gradle` file, but still obviously necessary.
    // See:
    //  - https://stackoverflow.com/questions/25626757/correct-way-of-referencing-tools-jar-in-a-cross-platform-way/25628156#25628156
    compileOnly files(Optional
        .ofNullable((URLClassLoader) ToolProvider.getSystemToolClassLoader())
        .map(urlClassLoader -> urlClassLoader.getURLs()) // `.ifPresent()` doesn't work since it returns void
        .orElse(new String[] { "${javaLibDir}/tools.jar" })
    )

    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    testImplementation 'io.projectreactor:reactor-test'
}


def objectEntries(obj) {
    // See:
    //  - https://stackoverflow.com/questions/13400075/reflection-generic-get-field-value
    //  - https://www.geeksforgeeks.org/reflection-in-java
    //  - https://docs.oracle.com/javase/8/docs/api/java/lang/Class.html#getDeclaredField-java.lang.String-
    Class<?> objClass = obj.getClass();
    Field[] objFields = objClass.getDeclaredFields();

    // Alternatives:
    // - Arrays.asList(objFields)
    Map<String, String> entriesMap = new HashMap<>();

    for (Field field : objFields) {
        String fieldSuffix = field.toString().replaceAll("(^.*)(\\.)([^\\.]+)\$", "\$3");

        field.setAccessible(true);
        entriesMap.put(fieldSuffix, field.get(obj));
    }

    return entriesMap;
}

def cliCmd(String[] commandAndArgs) {
    // Alternatives:
    //  - org.apache.commons.lang.StringUtils.join()
    //
    // See:
    //  - https://stackoverflow.com/questions/159148/groovy-executing-shell-commands
    //  - https://stackoverflow.com/questions/52389713/execute-bash-command-in-a-gradle-function
    String cmd = Arrays.stream(commandAndArgs)
        .map(String::valueOf)
        .collect(Collectors.joining(" "))
    ;

    // Alternatives:
    //  - commandLine(cmd)
    //  - executeCommand(cmd)
    //  - (Below with or without `.waitFor()`)
    //  - new String(cmd.execute()[.getOutputStream()|.toByteArray()][.toByteArray()])
    //  - They may or may not require:
    //      + .text.trim()
    //      + .consumeProcessOutput(stdOut, stdErr) // Assuming the args are instances of StringBuilder
    //      + .waitForProcessOutput(stdOut, stdErr)
    //      + new OutputStreamWriter(subProcess.[functionFromAbove]()).toString()
    def subProcess = cmd.execute();
    def subProcessStdOut = subProcess.getText();

    return subProcessStdOut;
}


if (System.properties['jfxrtDir'] == null) { // if not in root-level gradle.properties
    System.properties['jfxrtDir'] = '.'
}

def searchFile(Map<String, Closure> places, List<String> searchPaths, String searchID) {
    File result = null;
    places.each { k, v ->
        if (result != null) return;
        project.logger.debug("Looking for $searchID in $k")
        def dir = v()
        if (dir == null) {
            project.logger.debug("$k not set")
        } else {
            project.logger.debug("$k is $dir")
            searchPaths.each { s ->
                if (result != null) return;
                File f = new File(dir, s);
                project.logger.debug("Trying $f.path")
                if (f.exists() && f.file) {
                    project.logger.debug("found $searchID as $result")
                    result = f;
                }
            }
        }
    }
    if (!result?.file) {
        throw new GradleException("Could not find $searchID, please set one of ${places.keySet()}");
    } else {
        project.logger.info("$searchID: ${result}")
        return result
    }
}

def findJavaFxJar() {
    return searchFile(
        [
            'jfxrtDir in Gradle Properties': { System.properties['jfxrtDir'] },
            'JFXRT_HOME in System Environment': { System.getenv('JFXRT_HOME') },
            'JAVA_HOME in System Environment': { System.getenv('JAVA_HOME') },
            'java.home in JVM properties': { System.properties['java.home'] }
        ],
        [
            'jfxrt.jar',
            'lib/jfxrt.jar',
            'lib/ext/jfxrt.jar',
            'jre/lib/jfxrt.jar',
            'jre/lib/ext/jfxrt.jar'
        ],
        'JavaFX Runtime Jar'
    )
}

def findAntJavaFxJar() {
    return searchFile(
        [
            'jfxrtDir in Gradle Properties': { System.properties['jfxrtDir'] },
            'JFXRT_HOME in System Environment': { System.getenv('JFXRT_HOME') },
            'JAVA_HOME in System Environment': { System.getenv('JAVA_HOME') },
            'java.home in JVM properties': { System.properties['java.home'] }
        ],
        [
            'ant-javafx.jar',
            'lib/ant-javafx.jar',
            '../lib/ant-javafx.jar'
        ],
        'JavaFX Packager Tools'
    )
}

def getJavaFxJarPath() {
    return file(findJavaFxJar()).absolutePath
}

def copyDbToBuildDir() {
    copy {
        from dbName
        into warOutputDir
    }

    cliCmd("cd ${warOutputDir}", "chmod a+rwx *")

    println("Copied ${dbName} to ${warOutputDir}")
}


def buildFrontendIfNotPresent() {
    // Alternatives for printing the `project` object:
    //  - import groovy.json.JsonBuilder[.toPrettyString(project)]
    //  - import groovy.json.JsonOutput[.prettyPrint(project)]
    def serverWebAssetDir = new File(serverWebAssetDirPath);

    if (!serverWebAssetDir.exists()) {
        println("Client build output directory ${serverWebAssetDirPath} doesn't exist. Building the front-end now...");

        serverWebAssetDir.mkdirs();

        def clientDirDeps = new File(clientDirDepsPath);

        if (!clientDirDeps.exists()) {
            println("Client-side dependencies directory ${clientDirDepsPath} doesn't exist. Creating it now...");

            clientDirDeps.mkdirs();
            cliCmd(
                "cd ${rootDirPath}",
                "which node &>/dev/null || ./install-nvm.sh",
                "cd ${clientDirPath}",
                "npm install",
                "npm run build"
            );
        }
    }
}

tasks.withType(Copy).all {
    duplicatesStrategy 'include'
}

bootRun {
    // support passing -Dsystem.property=value to bootRun task
    systemProperties = System.properties
}

// WAR is needed because JAR doesn't include resources/public/
// Spring disables `war` task and replaces it with `bootWar`
bootWar {
    archiveBaseName = project.name
    archiveVersion = project.version

    if (injectJavaFxJar.toBoolean()) {
        /*
         * Add JavaFX's .jar file to classpath to bundle it with the app.
         *
         * Allows website hosting systems that don't support JavaFX to
         * run this webapp.
         *
         * Could also be done in dependencies: `implementation project.files(javaFxAbsolutePath)`
         * but was put here instead to keep the actual app's dependencies separated from
         * the dependencies that *should* already be on the host's system.
         */
        def javaFxAbsolutePath = getJavaFxJarPath()
        classpath(javaFxAbsolutePath)
        println("Injected JavaFX .jar file to dependencies from ${javaFxAbsolutePath}")
    }

    doFirst {
        buildFrontendIfNotPresent();
    }

    doLast {
        copyDbToBuildDir();
    }
}

// Creates a JAR that can run by itself instead of a WAR that requires running in a Tomcat container
//bootJar {
//    archiveBaseName = project.name
//    archiveVersion = project.version
//}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
    }
}

//Run specific test using `gradle test --tests <TestClassName, e.g. _11_>`
test {
    // use JUnit
    useJUnitPlatform()

    // Log if a test suite passed/failed
    testLogging {
        events "passed", "failed", "skipped"
    }

    // Silence 'Build failed, run with --stacktrace' messages
    ignoreFailures = true

    // Summarize results of each test suite (or test file if no suites specified)
    afterSuite { suite, result ->
        if (!suite.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
